/**
 * Copyright (c) 2019 by Delphix. All rights reserved.
 */

plugins {
    id "delphix.python"
}

artifacts {
    python sdist.distFile
}

dependencies {
    // Necessary to compile generated python protocol buffer libraries.
    python project(path: ":common", configuration: "python")
    python project(path: ":platform", configuration: "python")
    python project(path: ":libs", configuration: "python")
    python project(path: ":tools", configuration: "python")
}

dlpxPython {
    sources {
        delphix {
            url = "https://artifactory.delphix.com/artifactory/api/pypi/delphix-virtual-pypi/simple/"
        }
    }

    dist {
        name = "dvp"
    }

    packages {
        "dvp-common" {
            version = "== $project.version"
            path = file(tasks.getByPath(":common:sdist").getDistFile().toString())
        }

        "dvp-platform" {
            version = "== $project.version"
            path = file(tasks.getByPath(":platform:sdist").getDistFile().toString())
        }

        "dvp-libs" {
            version = "== $project.version"
            path = file(tasks.getByPath(":libs:sdist").getDistFile().toString())
        }

        "dvp-tools" {
            version = "== $project.version"
            path = file(tasks.getByPath(":tools:sdist").getDistFile().toString())
        }
    }

    supportedPythons {
        "python2.7" {}
    }
}

/*
 * As part of running a packages 'setup.py' file, setuptools creates 'egg-info' directories that contain information
 * about the build distribution. These can sometimes cause issues. We should probably build Python distributions in
 * 'build' so these would be created there, however they still could be created in the 'src' directory if someone runs
 * 'setup.py' manually. This is often done during development to install the package for testing.
 */
task removeEggInfo(type: Delete) {
    delete "${projectDir}/src/main/python/dvp.egg-info"
}

task wheel(type: SetupPyTask) {
    setupPyCommand "bdist_wheel"
    distFile String.format("%s-%s-%s-%s-%s.whl", dist.name.get().replace("-", "_"), "$project.version".replace("-", "_"), "py2", "none", "any")

    dependsOn makeSetupPy
}

clean.dependsOn('removeEggInfo')
