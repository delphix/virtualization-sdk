/*
 * Copyright (c) 2019 by Delphix. All rights reserved.
 */

plugins {
  id "java"
  id "delphix.python"
}

artifacts {
  python sdist.distFile
}

dlpxPython {
  sources {
    delphix {
      url = "https://artifactory.delphix.com/artifactory/api/pypi/dvp-virtual-pypi/simple/"
    }
  }

  dist {
    name = "dvp-platform"
  }

  packages {
    "dvp-api" {
      version = "== $project.ext.dvpApiVersion"
    }

    "dvp-common" {
      version = "== $project.version"
      path = file(tasks.getByPath(":common:sdist").getDistFile().toString())
    }

    enum34 {
      markers = "python_version < '3.4'"
    }
  }

  devPackages {
    mock {
      version = ">=2.0"
    }
  }

  supportedPythons {
    "python2.7" {}
  }
}

/*
 * As part of running a packages 'setup.py' file, setuptools creates 'egg-info' directories that contain information
 * about the build distribution. These can sometimes cause issues. We should probably build Python distributions in
 * 'build' so these would be created there, however they still could be created in the 'src' directory if someone runs
 * 'setup.py' manually. This is often done during development to install the package for testing.
 */
task removeEggInfo(type: Delete) {
  delete "${projectDir}/src/main/python/dvp_platform.egg-info"
}

task wheel(type: SetupPyTask) {
  setupPyCommand "bdist_wheel"
  distFile String.format("%s-%s-%s-%s-%s.whl", dist.name.get().replace("-", "_"), "$project.version".replace("-", "_"), "py2", "none", "any")

  dependsOn makeSetupPy
}

clean.dependsOn('removeEggInfo')

project.afterEvaluate {
  tasks["sdist"]
  tasks["test_python2.7"]
}
