/**
 * Copyright (c) 2019 by Delphix. All rights reserved.
 */

plugins {
  id "delphix.python"
}

def devVenvDir = project.layout.buildDirectory.file("test-venvs/python2.7")
def bootstrapVEnvBinary = project.layout.buildDirectory.file("pipsi/bin/virtualenv").get().asFile

/*
 * DEVELOPMENT TASKS
 */

def devPythonBinary = "${buildDir}/test-venvs/python2.7/bin/python"

def htmlReportDir = "${buildDir}/html-coverage"
def htmlReportFiles = fileTree(dir: htmlReportDir,
        includes: [
                "**/*.html",
                "**/*.js",
                "**/*.png",
                "**/*.json",
                "**/*.css",
        ])


// The "check" task should also lint and format.
check {
  dependsOn 'lint'
  dependsOn 'format'
  dependsOn 'coverage'
}

// A convenience task that formats both source and test code.
task format() {
  dependsOn 'formatSrc'
  dependsOn 'formatTest'
  dependsOn 'sortSrcImports'
  dependsOn 'sortTestImports'
}

task coverage(type: Exec) {
  commandLine devPythonBinary, '-m', 'pytest', '--cov=src/main/python', "${projectDir}/src/test/python"

  environment([
          PYTHONPATH: "${projectDir}/src/main/python:${projectDir}/src/test/python",
  ])

  dependsOn "sync_python2.7"

  outputs.file("$buildDir/.coverage")
  outputs.upToDateWhen {false}
}

task runHtmlReport(type: Exec, dependsOn: 'coverage') {
  commandLine devPythonBinary, '-m', 'coverage', 'html', '--dir', "${buildDir}/html-coverage",
          '--include', "*/${project.name}/src/main/python/*"

  outputs.files(htmlReportFiles)
}

task lint() {
  dependsOn 'lintSrc'
  dependsOn 'lintTest'
}

task clearVEnv(type: Exec) {
  commandLine bootstrapVEnvBinary.path, devVenvDir.get().asFile.path, '--clear'

  dependsOn "sync_python2.7"
}

/*
 * As part of running a packages 'setup.py' file, setuptools creates 'egg-info' directories that contain information
 * about the build distribution. These can sometimes cause issues. We should probably build Python distributions in
 * 'build' so these would be created there, however they still could be created in the 'src' directory if someone runs
 * 'setup.py' manually. This is often done during development to install the package for testing.
 */
task removeEggInfo(type: Delete) {
    delete "${projectDir}/src/main/python/dvp_tools.egg-info"
}

clean.dependsOn('removeEggInfo')

/*
* BUILD TASKS
*/

/*
 * HELPER TASKS
 *
 * This are just wrappers around Python commands and need to be moved into
 * a plugin.
 */

def pySrc = "${projectDir}/src/main/python"
def pyTestSrc = "${projectDir}/src/test/python"

task formatSrc(type: Exec) {
  commandLine devPythonBinary, '-m', 'yapf', '--recursive', '--in-place', pySrc

  dependsOn "sync_python2.7"
}

task formatTest(type: Exec) {
  commandLine devPythonBinary, '-m', 'yapf', '--recursive', '--in-place', pyTestSrc

  dependsOn "sync_python2.7"
}

task sortSrcImports(type: Exec) {
  commandLine devPythonBinary, '-m', 'isort', '-rc', pySrc

  dependsOn "sync_python2.7"
}

task sortTestImports(type: Exec) {
  commandLine devPythonBinary, '-m', 'isort', '-rc', pyTestSrc

  dependsOn "sync_python2.7"
}

task lintSrc(type: Exec) {
  commandLine devPythonBinary, '-m', 'flake8', pySrc

  dependsOn "sync_python2.7"
}

task lintTest(type: Exec) {
  commandLine devPythonBinary, '-m', 'flake8', pyTestSrc

  dependsOn "sync_python2.7"
}

artifacts {
  python sdist.distFile
}

dlpxPython {
  sources {
    delphix {
      url = "https://artifactory.delphix.com/artifactory/api/pypi/delphix-virtual-pypi/simple/"
    }
  }

  dist {
    name = "dvp-tools"
  }

  packages {
    click {
      version = ">= 7.0"
    }

    protobuf {
      version = "== 3.6.1"
    }

    pyyaml {
      version = ">= 3"
    }

    jsonschema {
      version = ">= 3"
    }

    requests {
      version = ">= 2.21.0"
    }

    typing {
      markers = "python_version < '3.5'"
    }

    jinja2 {
      version = ">= 2.10"
    }

    enum34 {
      version = ">= 1.1.6"
    }

  }

  devPackages {
    coverage {
      version = ">= 4.5"
    }

    flake8 {
      version = ">= 3.6"
    }

    httpretty {
      version = ">= 0.9.6"
    }

    isort {
      version = ">= 4.3"
    }

    mock {
      version = ">=2.0"
    }

    pytestCov {
      dist = "pytest-cov"
      version = ">= 2.6"
    }

    yapf {
      version = ">= 0.25"
    }

    /*
     * Following dvp-* packages are just meant for dev/test envs so that
     * dependent packages are available in path when running unit tests
     * under the tools package. These packages should never be production
     * dependencies as they need to be shipped independently.
     */
    "dvp-common" {
      version = "== $project.version"
      path = file(tasks.getByPath(":common:sdist").getDistFile().toString())
    }

    "dvp-platform" {
      version = "== $project.version"
      path = file(tasks.getByPath(":platform:sdist").getDistFile().toString())
    }

    "dvp-libs" {
      version = "== $project.version"
      path = file(tasks.getByPath(":libs:sdist").getDistFile().toString())
    }
  }

  supportedPythons {
    "python2.7" {}
  }
}

task "testDelphixBin_python2.7"(type: Exec) {
  executable "${buildDir}/install-test-venvs/python2.7/bin/dvp"
  args "--help"

  dependsOn "testInstallation_python2.7"
}

// sdist and the python tests depend on the generated python libraries
// from the protoc plugin. Must manually specify as plugins are not aware
// of each other.
project.afterEvaluate {
  tasks["test"].dependsOn tasks["testDelphixBin_python2.7"]
}
